loadEnvSpecificConfig()

apply plugin: "war"
apply plugin: "eclipse-wtp"
apply plugin: "groovy"
apply plugin: "java"
apply plugin: "jacoco"

ext {
 	springVersion = "4.1.6.RELEASE"
 	springSecurityVersion = "4.0.0.RELEASE"
 	tomcatVersion = "7.0.54"
 	hibernateVersion = "4.3.8.Final"
 	atomikosVersion = "3.9.3"
 	activeMqVersion = "5.10.0"
 	slf4jVersion ="1.7.7"
 	log4jVersion ="2.0.1"
 	mysqlVersion = mysqlVersion_ext // refer below buildScript block, to modify shared variable
 	flywayVersion = flywayVersion_ext // refer below buildScript block, to modify shared variable
 	httpClientVersion = "4.3.5"
 	jamonVersion = "2.81"
 	jacksonVersion = "2.5.2"
 	db = "oracle"
}

jacoco {
	toolVersion = "0.7.0.201403182114"
}

def cargoHome = "$buildDir/cargo"

targetCompatibility = 1.8
sourceCompatibility = 1.8

configurations.all {
	resolutionStrategy {
		failOnVersionConflict()
		force "org.springframework:spring-context:${springVersion}", "org.springframework:spring-beans:${springVersion}","org.springframework:spring-core:${springVersion}",
		"org.springframework:spring-expression:${springVersion}","org.springframework:spring-aop:${springVersion}","org.springframework:spring-web:${springVersion}",
		"javax.servlet:javax.servlet-api:3.1.0","xml-apis:xml-apis:1.3.03", "org.springframework:spring-jdbc:${springVersion}","org.springframework:spring-tx:${springVersion}", 
		"commons-logging:commons-logging:1.1.3","org.springframework.security:spring-security-core:${springSecurityVersion}","org.springframework:spring-webmvc:${springVersion}",
		"org.springframework.security:spring-security-config:${springSecurityVersion}","org.springframework.security:spring-security-web:${springSecurityVersion}",
		"commons-codec:commons-codec:1.6", "org.apache.httpcomponents:httpclient:${httpClientVersion}", "com.fasterxml:classmate:1.1.0", "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}",
		"javax.activation:activation:1.1.1", "org.slf4j:slf4j-api:${slf4jVersion}", "com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}",
		"com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
	}
	
}

repositories {
    mavenCentral()
    jcenter()
}

sourceSets {
	integration {
		java {
			compileClasspath += main.output
			runtimeClasspath += main.output
		}
	}
	main {
		java {
			output.classesDir="src/main/webapp/WEB-INF/classes"
		}
	}
}

configurations {
	integrationCompile.extendsFrom testCompile
	integrationRuntime.extendsFrom testRuntime
	tomcatRuntime
}

dependencies {
	tomcatRuntime "javax.transaction:javax.transaction-api:1.2",
		   "com.atomikos:transactions:${atomikosVersion}",
		   "com.atomikos:transactions-hibernate3:${atomikosVersion}",
		   "org.apache.activemq:activemq-all:${activeMqVersion}",
		   "commons-pool:commons-pool:1.6", "com.jamonapi:jamon:${jamonVersion}"
		   
	compile "commons-io:commons-io:2.4"
	compile "org.springframework:spring-context:${springVersion}"
	compile "org.springframework:spring-orm:${springVersion}"
	compile "org.springframework:spring-webmvc:${springVersion}"
	compile "org.springframework:spring-jms:${springVersion}"
	
	compile "org.springframework:spring-context-support:${springVersion}"

	compile "org.springframework.security:spring-security-config:${springSecurityVersion}"
	compile "org.springframework.security:spring-security-web:${springSecurityVersion}"
	compile "org.springframework.security:spring-security-acl:${springSecurityVersion}"
	compile ("net.sf.ehcache:ehcache:2.8.3") {
		exclude group: "org.slf4j"
	}
	
	compile("org.hibernate:hibernate-entitymanager:${hibernateVersion}") {
		exclude group: "org.jboss.spec.javax.transaction"
	}
	
	compile "org.apache.httpcomponents:httpclient:${httpClientVersion}"

	providedCompile "com.atomikos:transactions:${atomikosVersion}"
	providedCompile "javax.transaction:javax.transaction-api:1.2"
	providedCompile "com.atomikos:transactions-hibernate3:${atomikosVersion}" 
	providedCompile "org.slf4j:jcl-over-slf4j:${slf4jVersion}"
	providedCompile "mysql:mysql-connector-java:${mysqlVersion}"
	providedCompile "org.apache.activemq:activemq-all:${activeMqVersion}"
	providedCompile "javax.servlet:javax.servlet-api:3.1.0", "javax.servlet:jsp-api:2.0"
	providedCompile "commons-pool:commons-pool:1.6"
	providedCompile "com.jamonapi:jamon:${jamonVersion}"
	
	
	compile "org.apache.commons:commons-lang3:3.3.2"
	compile "org.slf4j:slf4j-api:${slf4jVersion}"
	compile "org.slf4j:slf4j-log4j12:${slf4jVersion}"
	compile "org.apache.logging.log4j:log4j-1.2-api:${log4jVersion}"
	compile "org.apache.logging.log4j:log4j-slf4j-impl:${log4jVersion}"
	
	compile "org.hibernate:hibernate-validator:5.1.3.Final"
	compile "javax.inject:javax.inject:1"
	
	//jackson
	compile "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
	compile "com.fasterxml.jackson.datatype:jackson-datatype-hibernate4:${jacksonVersion}"
	compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonVersion}"
	
	compile "org.openid4java:openid4java:0.9.8"
	compile "javax.ws.rs:jsr311-api:1.1.1"
	compile "jstl:jstl:1.2"
	compile "org.apache.commons:commons-collections4:4.0"
	compile "org.springframework.security.oauth:spring-security-oauth2:2.0.7.RELEASE"
	compile "org.springframework.security:spring-security-jwt:1.0.2.RELEASE"
	
	//mail
	compile "javax.mail:mail:1.4.7"
	compile "javax.activation:activation:1.1.1"
	compile "org.freemarker:freemarker:2.3.22"
	
	//swagger
	//compile "org.ajar:swagger-spring-mvc-ui:0.4"
	compile "io.springfox:springfox-swagger2:2.0.0"
	
	testCompile "junit:junit:4.12"
	testCompile "org.mockito:mockito-all:1.10.19"
	testCompile "org.hamcrest:hamcrest-core:1.3"
	testCompile "org.hamcrest:hamcrest-library:1.3"
	testCompile "org.springframework:spring-test:${springVersion}"
	testCompile "mysql:mysql-connector-java:${mysqlVersion}"
	testCompile files("lib/oracle/ojdbc6.jar")
	
	integrationCompile sourceSets.main.output
	integrationCompile configurations.testCompile
	integrationCompile sourceSets.test.output
	integrationRuntime configurations.testRuntime
}

task integrationTest(type: Test, dependsOn: jar) {
	testClassesDir = sourceSets.integration.output.classesDir
	classpath = sourceSets.integration.runtimeClasspath
	systemProperties["jar.path"] = jar.archivePath
	
	jacoco {
		excludes = ["com.att.developer.**.*Test*", "com.att.developer.config.IntegrationContext*"]
	}
}

test {
	jacoco {
		excludes = ["com.att.developer.**.*Test*", "com.att.developer.bean.builder.*", "com.att.developer.config.*"]
	}
}

/**
* Manual task to manage tomcat lib dependencies
*/
task manageTomcatLib << {
  [ 'tomcatRuntime' ].each { mode ->
    copy {
      from configurations.getByName(mode).files
      into file('/lib/tomcat')
    }
  }
}

check.dependsOn integrationTest

def loadEnvSpecificConfig() {
	def environment = project.hasProperty("env") ? project.env : "local"
	println "Environment is set to $environment"
	def envSpecFile = file("config/env.json")
	def slurper = new groovy.json.JsonSlurper();
	def config = slurper.parseText(envSpecFile.text)
	def prop = config[environment]
	println "config $prop"
	project.ext.set("env", prop)
}

/** Flyway settings */

buildscript {

	ext {
 		mysqlVersion_ext = "5.1.31"
 		flywayVersion_ext = "3.2.1"
	}
	
	repositories {
		mavenCentral()
		flatDir name: 'localRepository', dirs: 'lib/oracle'
	}
	
	dependencies {
		// TODO - fix it after groovy support for 1.8 is available
		classpath "org.flywaydb:flyway-gradle-plugin:${flywayVersion_ext}"
		classpath "mysql:mysql-connector-java:${mysqlVersion_ext}"
		classpath name:"ojdbc6"
	}
}

apply plugin: "org.flywaydb.flyway"

flyway {
	url = env.flyway."${db}".url
	user = env.flyway."${db}".user
	password = env.flyway."${db}".password
	schemas = env.flyway."${db}".schemas
	baselineVersion = env.flyway."${db}".version
	locations = env.flyway."${db}".locations
}

/** Tomcat settings */

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-cargo-plugin:2.1.1'
    }
}

apply plugin: 'com.bmuschko.cargo'

cargo {
    containerId = 'tomcat7x'
    port = 9080
	
    deployable {
        context = 'developer'
    }

    local {
      installer {
            installUrl = 'http://mirrors.koehn.com/apache/tomcat/tomcat-7/v7.0.62/bin/apache-tomcat-7.0.62-windows-x64.zip'
            downloadDir = file("$buildDir/download")
            extractDir = file("$buildDir/extract")
        }
        
        homeDir = file(cargoHome)
        configHomeDir = file(cargoHome)
        logLevel = "medium"
        
		extraClasspath = fileTree(dir: 'lib', excludes: ['test/**']);
		
		jvmArgs = "-Xdebug -Xrunjdwp:transport=dt_socket,address=8787,server=y,suspend=n -Xnoagent -Djava.compiler=NONE"
		
		configFile {
            file = file('src/main/webapp/resources/tomcat/developer.xml')
            toDir = new File("conf/Catalina/localhost")
        }
		
        timeout = 60000

        containerProperties {
            property 'cargo.tomcat.ajp.port', 9099
        }
    }
}

task createCargoHome() {
	doLast {
		if (!file(cargoHome).exists() && !file(cargoHome).mkdirs()) {
			println "Failed to create directory '${cargoHome}'"
		}
	}
}
 
cargoRunLocal.dependsOn createCargoHome 

/** Tomcat end of settings */

task jacocoIntegrationTestReport(type: JacocoReport) {
	sourceSets sourceSets.main
	executionData integrationTest
}

/**
 * Grunt Related Tasks
 * 
 */

build.dependsOn ':client:grunt_build'
war.dependsOn ':client:grunt_war'
war.baseName = 'developer'


eclipse {
	project {
		name = 'server'
		natures.add "org.springframework.ide.eclipse.core.springnature"
		natures.add "org.springsource.ide.eclipse.gradle.core.nature"
		natures.add "org.eclipse.jdt.core.javanature"
		natures.add "org.eclipse.jdt.groovy.core.groovyNature"
		natures.add "org.eclipse.wst.common.project.facet.core.nature"
		natures.add "org.eclipse.wst.common.modulecore.ModuleCoreNature"
		natures.add "org.eclipse.jem.workbench.JavaEMFNature"
		
		buildCommand "org.eclipse.jdt.core.javabuilder"
		buildCommand "org.eclipse.wst.common.project.facet.core.builder"
		buildCommand "org.eclipse.wst.validation.validationbuilder"
		buildCommand "org.springframework.ide.eclipse.core.springbuilder"
	}
	
	classpath {
		file {
			whenMerged { classpath ->
				classpath.entries.removeAll { entry -> true }
			}
			withXml {
				Node node = it.asNode()
				node.appendNode("classpathentry",[kind:"src",output:"bin",path:"src/integration/java"])
				node.appendNode("classpathentry",[kind:"src",output:"bin",path:"src/integration/resources"])
				node.appendNode("classpathentry",[kind:"src",path:"src/main/java"])
				node.appendNode("classpathentry",[kind:"src",path:"src/main/resources"])
				node.appendNode("classpathentry",[kind:"src",output:"bin",path:"src/test/java"])
				node.appendNode("classpathentry",[kind:"src",output:"bin",path:"src/test/resources"])
				node.appendNode("classpathentry",[exported:"true",kind:"con",path:"org.eclipse.jdt.launching.JRE_CONTAINER"])
				node.appendNode("classpathentry",[exported:"true",kind:"con",path:"org.eclipse.jst.j2ee.internal.web.container"])
				node.appendNode("classpathentry",[exported:"true",kind:"con",path:"org.springsource.ide.eclipse.gradle.classpathcontainer"]).appendNode('attributes').appendNode('attribute', [name:"org.eclipse.jst.component.dependency", value:"/WEB-INF/lib"])
				node.appendNode("classpathentry",[kind:"output",path:"src/main/webapp/WEB-INF/classes"])
				
			}
		}
	}
}

