
loadEnvSpecificConfig()

apply plugin: 'war'
apply plugin: 'eclipse-wtp'
apply plugin: 'groovy'
apply plugin: 'tomcat'
apply plugin: 'java'

targetCompatibility = 1.7
sourceCompatibility = 1.7

configurations.all {
	resolutionStrategy {
		failOnVersionConflict()
		force 'org.springframework:spring-context:4.0.2.RELEASE', 'org.springframework:spring-beans:4.0.2.RELEASE','org.springframework:spring-core:4.0.2.RELEASE',
		'org.springframework:spring-expression:4.0.2.RELEASE','org.springframework:spring-aop:4.0.2.RELEASE','org.springframework:spring-web:4.0.2.RELEASE',
		'javax.servlet:servlet-api:2.5'
	}
	exclude group: 'commons-logging'
}

repositories {
    mavenCentral()
}

sourceSets {
	integration {
		java {
			compileClasspath += main.output
			runtimeClasspath += main.output
		}
	}
	main {
		java {
			output.classesDir='src/main/webapp/WEB-INF/classes'
		}
	}
}

dependencies {
	def tomcatVersion = '7.0.42'
	tomcat "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}",
		   "org.apache.tomcat.embed:tomcat-embed-logging-juli:${tomcatVersion}",
		   "javax.transaction:javax.transaction-api:1.2",
		   "com.atomikos:transactions:3.9.3",
		   "com.atomikos:transactions-hibernate3:3.9.3"
	
	tomcat files("lib/atomikos/atomikos-integration-extension.jar", "lib/atomikos/transactions.properties")
    //tomcat files("lib/atomikos/atomikos-integration-extension.jar", "lib/atomikos/atomikos-util-3.9.3.jar", "lib/atomikos/transactions-3.9.3.jar"
	//	, "lib/atomikos/transactions-api-3.9.3.jar", "lib/atomikos/transactions-hibernate3-3.9.3.jar", "lib/atomikos/transactions-jdbc-3.9.3.jar", "lib/atomikos/transactions-jta-3.9.3.jar")
				
	tomcat("org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}") {
		exclude group: 'org.eclipse.jdt.core.compiler', module: 'ecj'
	}
	
	compile group: 'commons-io', name: 'commons-io', version: '1.4'
	compile 'org.springframework:spring-context:4.0.2.RELEASE'
	compile 'org.springframework:spring-orm:4.0.2.RELEASE'
	compile 'org.springframework:spring-webmvc:4.0.2.RELEASE'
	//compile 'org.springframework:spring-tx:4.0.2.RELEASE'

	compile 'org.springframework.security:spring-security-config:3.2.1.RELEASE'
	compile 'org.springframework.security:spring-security-web:3.2.1.RELEASE'
	
	compile('org.hibernate:hibernate-entitymanager:4.3.4.Final') {
		exclude group: 'org.jboss.spec.javax.transaction'
	}
	
	providedCompile 'javax.transaction:javax.transaction-api:1.2'
	providedCompile 'com.atomikos:transactions-hibernate3:3.9.3'
	providedCompile 'org.slf4j:jcl-over-slf4j:1.7.6'
	providedCompile 'javax.servlet:servlet-api:2.5', 'javax.servlet:jsp-api:2.0'
	
	compile 'org.apache.commons:commons-lang3:3.3'
	compile 'org.slf4j:slf4j-api:1.7.6'
	compile 'org.slf4j:slf4j-log4j12:1.7.6'
	compile 'org.hibernate:hibernate-validator:5.1.0.Final'
	compile 'javax.inject:javax.inject:1'
	compile 'com.fasterxml.jackson.core:jackson-databind:2.3.2'
	
	testCompile 'junit:junit:4.11'
	testCompile 'org.mockito:mockito-all:1.9.5'
	testCompile 'org.springframework:spring-test:4.0.2.RELEASE'
	testCompile 'mysql:mysql-connector-java:5.1.21'
	
	integrationCompile sourceSets.main.output
	integrationCompile configurations.testCompile
	integrationCompile sourceSets.test.output
	integrationRuntime configurations.testRuntime
}

task integrationTest(type: Test, dependsOn: jar) {
	testClassesDir = sourceSets.integration.output.classesDir
	classpath = sourceSets.integration.runtimeClasspath
	systemProperties['jar.path'] = jar.archivePath
}

check.dependsOn integrationTest

def loadEnvSpecificConfig() {
	def environment = project.hasProperty('env') ? project.env : 'local'
	println "Environment is set to $environment"
	def envSpecFile = file('/config/env.json')
	def slurper = new groovy.json.JsonSlurper();
	def config = slurper.parseText(envSpecFile.text)
	def prop = config[environment]
	println "config $prop"
	project.ext.set('env', prop)
}

/** Flyway settings */

buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath "com.googlecode.flyway:flyway-gradle-plugin:2.3"
		classpath 'mysql:mysql-connector-java:5.1.29'
	}
}

apply plugin: 'flyway'

println "flyway.url = $env.flyway.url"
println "flyway.url = $env.flyway.schemas"

flyway {
	url = env.flyway.url
	user = env.flyway.user
	password = env.flyway.password
	schemas = env.flyway.schemas
	initVersion = env.flyway.version
}

/** Tomcat settings */

buildscript {
  repositories {
    mavenCentral()
  }

  dependencies {
    classpath 'org.gradle.api.plugins:gradle-tomcat-plugin:0.9.8'
  }
}

tomcatRun {
	httpPort = 9080
	httpsPort = 9443
	//ajpPort=9001
	stopPort=9008
	enableSSL = false
	reloadable=true
}

tomcatRunWar {
	httpPort = 9080
	httpsPort = 9443
	//ajpPort=9001
	stopPort=9008
	enableSSL = false
}

/*eclipse {
	project {
		natures.add 'org.eclipse.jdt.groovy.core.groovyNature'
		natures.add 'org.springsource.ide.eclipse.gradle.core.nature'
	}
	classpath {
		file {
			withXml {
				Node node = it.asNode()
				node.appendNode('classpathentry',[exported:"true",kind:"con",path:"GROOVY_SUPPORT"])
				node.appendNode('classpathentry',[exported:"true",kind:"con",path:"GROOVY_DSL_SUPPORT"])
			}
			whenMerged { classpath ->
				classpath.entries.findAll { it.path.contains('JRE_CONTAINER') }.each {
					it.path += "/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.7"
				}
			}
		}
	}
}*/